REFINEMENT Pdpa
REFINES AbstractContract

CONSTANTS
  // actors
  org, indiv, pdpc,
  // rule names
  Assessment, NotifyPdpc, NotifyIndiv, ProhibitNotifyIndiv, CannotNotifyIndiv,
  // event names
  assess, notify_pdpc, notify_indiv, prohibit_notify_indiv

DEFINITIONS
  "Defs.def"

PROPERTIES
  /*
  Remember that writing {a, b} doesn't make a and b different from each other.
  Hence we need to use cardinality constraints to encode the fact that
  all the elements are distinct.
  */
  card(Actor) = 3 ∧
  Actor = {org, indiv, pdpc} ∧

  card(RuleName) = 5 ∧
  RuleName = {
    Assessment, NotifyPdpc, NotifyIndiv, ProhibitNotifyIndiv, CannotNotifyIndiv
  } ∧

  card(EventName) = 4 ∧
  EventName = {
    assess, notify_pdpc, notify_indiv, prohibit_notify_indiv
  } ∧

  Rule = {
    rec(
      name : Assessment,
      possible_actors : {org},
      initial_actors: {org},
      deontic : NCOb,
      event : rec(
        name : assess,
        is_preemptive : TRUE,
        initial_timer_val : 30
      ),
      on_done : rec(
        trigger :
          λ actor. (actor ∈ Actor | {(NotifyIndiv, actor), (NotifyPdpc, actor)}),
        interrupt :
          λ actor. (actor ∈ Actor | ∅)
      ),
      on_timeout : rec(
        trigger :
          λ actor. (actor ∈ Actor | ∅),
        interrupt :
          λ actor. (actor ∈ Actor | ∅)
      )
    ),

    rec(
      name : NotifyPdpc,
      possible_actors : {org},
      initial_actors : ∅,
      deontic : NCOb,
      event : rec(
        name : notify_pdpc,
        is_preemptive : TRUE,
        initial_timer_val : 3
      ),
      on_done : rec(
        trigger :
          λ actor. (actor ∈ Actor | {(ProhibitNotifyIndiv, pdpc)}),
        interrupt :
          λ actor. (actor ∈ Actor | ∅)
      ),
      on_timeout : rec(
        trigger :
          λ actor. (actor ∈ Actor | ∅),
        interrupt :
          λ actor. (actor ∈ Actor | ∅)
      )
    ),

    rec(
      name : NotifyIndiv,
      possible_actors : {org},
      initial_actors : ∅,
      deontic : NCOb,
      event : rec(
        name : notify_indiv,
        is_preemptive : TRUE,
        initial_timer_val : 3
      ),
      on_done : rec(
        trigger :
          λ actor. (actor ∈ Actor | ∅),
        interrupt :
          λ actor. (actor ∈ Actor | ∅)
      ),
      on_timeout : rec(
        trigger :
          λ actor. (actor ∈ Actor | ∅),
        interrupt :
          λ actor. (actor ∈ Actor | ∅)
      )
    ),

    rec(
      name : ProhibitNotifyIndiv,
      possible_actors : {pdpc},
      initial_actors : ∅,
      deontic : Perm,
      event : rec(
        name : prohibit_notify_indiv,
        is_preemptive : TRUE,
        initial_timer_val : 5
      ),
      on_done : rec(
        trigger :
          λ actor. (actor : Actor | {(CannotNotifyIndiv, org)}),
        interrupt :
          λ actor. (actor : Actor | {(NotifyIndiv, org)})
      ),
      on_timeout : rec(
        trigger :
          λ actor. (actor ∈ Actor | ∅),
        interrupt :
          λ actor. (actor ∈ Actor | ∅)
      )
    ),

    rec(
      name : CannotNotifyIndiv,
      possible_actors : {org},
      initial_actors : ∅,
      deontic : NCProhib,
      event : rec(
        name : notify_indiv,
        is_preemptive : TRUE,
        initial_timer_val : 5
      ),
      on_done : rec(
        trigger :
          λ actor. (actor ∈ Actor | ∅),
        interrupt :
          λ actor. (actor ∈ Actor | {(NotifyIndiv, org)})
      ),
      on_timeout : rec(
        trigger :
          λ actor. (actor ∈ Actor | ∅),
        interrupt :
          λ actor. (actor ∈ Actor | ∅)
      )
    )
  }

INITIALISATION
  started := FALSE ||
  breached_by := ∅ ||
  active_rules := ∅

OPERATIONS
  assess_happened(actor) = event_happened(assess, actor);
  assess_timeout(actor) = event_timeout(assess, actor);

  notify_indiv_happened(actor) = event_happened(notify_indiv, actor);
  notify_indiv_timeout(actor) = event_timeout(notify_indiv, actor);

  notify_pdpc_happened(actor) = event_happened(notify_pdpc, actor);
  notify_pdpc_timeout(actor) = event_timeout(notify_pdpc, actor);

  prohibit_notify_indiv_happened(actor) = event_happened(prohibit_notify_indiv, actor);
  prohibit_notify_indiv_timeout(actor) = event_timeout(prohibit_notify_indiv, actor)

END