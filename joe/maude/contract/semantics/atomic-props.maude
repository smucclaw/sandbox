---(
  Atomic propositions for model checking.
)
load model-checker
load semantics

(omod ATOMIC-PROPS is
  protecting SEMANTICS .
  including MODEL-CHECKER + SATISFACTION . 

  subsorts Configuration < State .
  subsorts ActionEvent < Prop .

  --- ops Top : -> Prop .
  ops _mustDo_ _mayDo_ _allowedToDo_ _prohibitedFromDoing_ : Qid Qid -> Prop .
  --- ops raceCond_ : ActionEvent -> Formula .

  vars config : Configuration .
  vars actorName actionName coordinator ruleName : Qid .
  vars hence lest : Set{Qid} .
  vars deontic : Deontic .
  vars rules : Set{Rule} .
  vars activeRules : Set{Rule} .
  vars time : Nat .

---(
  The satisfaction relation has type
    _ |= _ : Configuration -> Prop -> 2
)
  --- eq config |= Top
  --- = true .

  eq (actorName does actionName) config |= actorName does actionName
  = true .

  eq
    < coordinator : Coordinator |
      contractState : Active (
        RULE ruleName
        PARTY actorName
        MUST DO actionName
        WITHIN time DAY
        HENCE hence
        LEST lest,
        activeRules
      ) > config
    |= actorName mustDo actionName
  = true .

  eq
    < coordinator : Coordinator |
      contractState : Active (
        RULE ruleName
        PARTY actorName
        MAY DO actionName
        WITHIN time DAY
        HENCE hence
        LEST lest,
        activeRules
      ) > config
    |= actorName mayDo actionName
  = true .

  eq config |= actorName allowedToDo actionName
  = config |= actorName mustDo actionName or
    config |= actorName mayDo actionName .

  eq
    < coordinator : Coordinator |
      contractState : Active (
        RULE ruleName
        PARTY actorName
        SHANT DO actionName
        WITHIN time DAY
        HENCE hence
        LEST lest,
        activeRules
      ) > config
    |= actorName prohibitedFromDoing actionName
  = true .

---(
  Need to loop through all actorName and actionName in a rule set and
  construct one such formula for each pair.
  Then take conjunction over all of them.

  eq raceCond (actorName does actionName) =
    (actorName does actionName) =>
      [] ~ (actorName prohibitedFromDoing actionName) .
)

endom)