---(
  Strategy module for detecting PDPA race condition.
)

load ../rewrite-trace-strat

(smod RACE-COND-STRAT is
  --- protecting SEMANTICS .
  protecting REWRITE-TRACE-STRAT .

  strat raceCond : Set{Rule} @ Configuration .
  strat raceCondAux : ActionEvent @ Configuration .

  vars actorName actionName ruleName coordinator : Qid .
  vars hence lest : Set{Qid} .
  vars time : Nat .
  vars deontic : Deontic .
  vars deadline : Deadline .
  vars activeRules rules : Set{Rule} .
  vars config : Configuration .

  sd raceCond(empty) := idle .

  sd raceCond(
    (
      RULE ruleName
      PARTY actorName
      deontic DO actionName
      deadline
      HENCE hence
      LEST lest,
      rules
    )
  ) := raceCondAux(actorName does actionName) | raceCond(rules) .

  sd raceCondAux(actorName does actionName) :=
    all * ;
    match config s.t.
      config |= actorName allowedToDo actionName ;
    rewriteEvent(actorName does actionName) ;
    all * ;
    match config s.t.
      config |= actorName prohibitedFromDoing actionName .

endsm)