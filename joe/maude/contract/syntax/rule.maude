--- view Set`{Qid`} from TRIV to SET{Qid} is
---   sort Elt to Set`{Qid`} .
--- endv

---(
  List -- quotient by associativity and commutativity of concat --> finite multiset
       -- quotient by idempotence of concat --> finite set 
)
fmod NAMES is
  protecting QID .
  protecting SET{Qid} .

  sort Names .
  subsorts Qid < Names < Set{Qid} .

  ops NOTHING : -> Names .
  ops _AND_ : Names Names -> Names
    [assoc comm id: empty prec 121 format (d r os d)] .

  vars names names' : Names .

  eq NOTHING = empty .
  eq names AND names' = names, names' .

endfm

fmod RULE is
  protecting BOOL .
  protecting STRING .
  protecting NAT .
  protecting NAMES .

  sorts
    Rule RuleName ActorName Deadline ActionName Deontic Date Duration
    Situation ActorDeonticActionDeadline HenceLest .

  --- Dynamic subtyping/subsorting.
  subsorts Qid Bool < Situation .

---(
  MUST -- obligation
  MAY -- permission
  SHANT -- prohibition
)
  ops MUST MAY SHANT : -> Deontic [ctor] .

  --- Yep we have operator overloading too.
  ops PARTY___DO_ : Qid Deontic Deadline Qid -> ActorDeonticActionDeadline
    [ctor] .
  ops PARTY___DO_ : Qid Deadline Deontic Qid -> ActorDeonticActionDeadline
    [ctor] .
  ops _PARTY__DO_ : Deadline Qid Deontic Qid -> ActorDeonticActionDeadline
    [ctor] .
  ops PARTY__DO__ : Qid Deontic Qid Deadline -> ActorDeonticActionDeadline
    [ctor] .

---(
  BY is for dates, ie absolute deadlines
  WITHIN is for relative deadlines
)
  ops BY_ : Nat -> Deadline [ctor].
  ops WITHIN_ : Duration -> Deadline [ctor] .

  ops _DAY _WEEK _MONTH _YEAR : Nat -> Duration [ctor] .

  --- op _ performs _ at _ : ActorName ActionName Date -> Event [ctor] .

  --- op RULE _ UPON _ WHILE _ PARTY _ _ DO _ _ HENCE _ LEST _ :
  ---   Qid Situation Situation Qid Deontic Qid Deadline
  ---   Situation Situation -> Rule [ctor] .

  ops HENCE_LEST_ : Set{Qid} Set{Qid} -> HenceLest [ctor] .
  ops LEST_HENCE_ : Set{Qid} Set{Qid} -> HenceLest [ctor] .
  ops HENCE_ : Set{Qid} -> HenceLest [ctor] .
  ops LEST_ : Set{Qid} -> HenceLest [ctor] .

  ops RULE___ :
    Qid ActorDeonticActionDeadline HenceLest -> Rule [ctor] .

  ops RULE__ :
    Qid ActorDeonticActionDeadline -> Rule [ctor] .

  vars ruleName actorName actionName : Qid .
  vars deontic : Deontic .
  vars deadline : Deadline .
  vars hence lest : Set{Qid} .

---(
  The following equations (universally quantified over the above first-order
  signature) are used to normalize terms into the following normal form, over
  which all our semantics is defined:
    RULE ruleName
    PARTY actorName
    deontic DO actionName
    deadline
    HENCE hence
    LEST lest
  In other words, we always pattern match against this normal form when doing
  primitive recursion aka folding.
)
  eq PARTY actorName deontic deadline DO actionName
  = PARTY actorName deontic DO actionName deadline .

  eq PARTY actorName deadline deontic DO actionName
  = PARTY actorName deontic DO actionName deadline .

  eq deadline PARTY actorName deontic DO actionName
  = PARTY actorName deontic DO actionName deadline .

  eq HENCE hence = HENCE hence LEST NOTHING .
  eq LEST lest = HENCE NOTHING LEST lest . 
  eq LEST lest HENCE hence = HENCE hence LEST lest .

  eq
    RULE ruleName
    PARTY actorName
    deontic DO actionName
    deadline
  = RULE ruleName
    PARTY actorName
    deontic DO actionName
    deadline
    HENCE NOTHING
    LEST NOTHING .

  --- eq
  ---   RULE ruleName
  ---   PARTY actorName
  ---   deontic DO actionName
  ---   deadline
  ---   HENCE hence
  --- = RULE ruleName
  ---   PARTY actorName
  ---   deontic DO actionName
  ---   deadline
  ---   HENCE hence
  ---   LEST NOTHING .

  --- eq
  ---   RULE ruleName
  ---   PARTY actorName
  ---   deontic DO actionName
  ---   deadline
  ---   LEST lest
  --- = RULE ruleName
  ---   PARTY actorName
  ---   deontic DO actionName
  ---   deadline
  ---   HENCE NOTHING
  ---   LEST lest .

endfm

view Rule from TRIV to RULE is
  sort Elt to Rule .
endv

view Set`{Rule`} from TRIV to SET{Rule} is
  sort Elt to Set`{Rule`} .
endv

--- fmod RULE-LIST is
---   protecting LIST{Rule} * (
---     sort NeList{Rule} to NeRuleList,
---     sort List{Rule} to RuleList
---   ) .
--- endfm