***(
  This is based on:
  https://www.mat.ufrn.br/cade-27/wp-content/uploads/2019/09/tutorial-RL.pdf
  http://subsell.logic.at/theorem-maude/
)

*** load full-maude.maude

*** Well-formed formulae.
fmod WFF is
  pr QID .
  sort Prop .
  sort Wff .
  subsort Qid < Prop < Wff .

  op _->_ : Wff Wff -> Wff [ctor assoc prec 30] .
  op ~_ : Wff -> Wff [prec 25] .

endfm

view Wff from TRIV to WFF is
  sort Elt to Wff .
endv

*** Contexts.
fmod Ctx is
  pr SET{Wff} * (
    sort Set{Wff} to Ctx,
    sort NESet{Wff} to NECtx
  ) .
endfm

*** Sequents.
fmod Sequent is
  pr Ctx .
  sorts Sequent Sequents .
  subsort Sequent < Sequents .

  op Leaf : -> Sequent [ctor] .
  op _|-_ : Ctx Ctx -> Sequent [ctor prec 122] .

  *** Meta-theoretic conjunction of judgement forms.
  op _ _ : Sequents Sequents -> Sequents [ctor comm assoc id: Leaf] .
endfm

***(
  Bottom-up proof search for Gentzen's LK.

  Thanks to cut elimination, the complexity of the active formulae induces a
  well-founded ordering on our search procedure, thus guaranteeing strong
  normalization.

  Would be interesting to adapt this to obtain a proof search procedure for
  nonmonotonic, defeasible logics.
)
mod LK is
  pr Sequent .
  vars P : Prop .
  vars Gamma Delta : Ctx .
  vars Phi Psi : Wff .

  rl [Axm] :
    Gamma, Phi |- Phi, Delta
*** -------------------------
      => Leaf .

  rl [ImpL] :
    Gamma, Phi -> Psi |- Delta
*** -------------------------------------------------
    => (Gamma |- Phi, Delta) (Gamma, Psi |- Delta) .

  rl [ImpR] :
    Gamma |- Phi -> Psi, Delta
*** ------------------------------- 
     => Gamma, Phi |- Psi, Delta .

  rl [NegL] :
    Gamma, ~ Phi |- Delta
*** ------------------------
    => Gamma |- Phi, Delta .

  rl [NegR] :
    Gamma |- ~ Phi, Delta
*** ------------------------
    => Gamma, Phi |- Delta .
endm

*** search [1] ~ 'P -> ~ 'Q |- 'Q -> 'P =>* Leaf .
*** show search graph .